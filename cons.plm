/****h* plm/cons
 *
 * NAME
 *   cons -- console input and output module
 *
 * DESCRIPTION
 *   This modules reads and writes data from and to the console.
 *
 ******
 */

cons:
do;

$include (d:iocld.src)

$include (globlit.inc)


    print$string: procedure (p$str) public;
        declare p$str pointer;
        declare chr based p$str byte;
        do while chr <> 0;
            call wr$con(chr);
            p$str = p$str + 1;
        end;
    end print$string;

    print$char: procedure (chr) public;
        declare chr byte;
        call wr$con(chr);
    end print$char;

    new$line: procedure public;
        call print$char(cr);
        call print$char(lf);
    end new$line;

    print$line: procedure (p$str) public;
        declare p$str pointer;
        call print$string(p$str);
        call new$line;
    end print$line;

    print$number: procedure (n) public;
        declare n uint;
        declare buf (10) byte;
        declare i uint;
        if n = 0 then
            call print$char('0');
        else do;
            i = length(buf) - 2;
            buf(i+1) = 0;
            do while n > 0;
                buf(i) = n mod 10 + '0';
                n = n / 10;
                i = i - 1;
            end;
            call print$string(.buf(i+1));
        end;
    end print$number;


end;
