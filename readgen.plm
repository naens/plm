readgen:
    do;

$include (d:iocld.src)

    declare pointer literally 'address';
    declare uint literally 'address';
    declare boolean literally 'byte';
    declare true literally '1';
    declare false literally '0';

    declare space literally '20h';
    declare tab literally '9h';
    declare lf literally '0ah';
    declare cr literally '0dh';

    declare tail$start literally '80h';

    print$string: procedure (p$str);
        declare p$str pointer;
        declare chr based p$str byte;
        do while chr <> 0;
            call wr$con(chr);
            p$str = p$str + 1;
        end;
    end print$string;

    print$char: procedure (chr);
        declare chr byte;
        call wr$con(chr);
    end print$char;

    new$line: procedure;
        call print$char(cr);
        call print$char(lf);
    end new$line;

    print$line: procedure (p$str);
        declare p$str pointer;
        call print$string(p$str);
        call new$line;
    end print$line;

    print$number: procedure (n);
        declare n uint;
        declare buf (10) byte;
        declare i uint;
        if n = 0 then
            call print$char('0');
        else do;
            i = length(buf) - 2;
            buf(i+1) = 0;
            do while n > 0;
                buf(i) = n mod 10 + '0';
                n = n / 10;
                i = i - 1;
            end;
            call print$string(.buf(i+1));
        end;
    end print$number;

    string$equal: procedure (p$str1, p$str2) boolean;
        declare (p$str1, p$str2) pointer;
        declare str1 based p$str1 byte;
        declare str2 based p$str2 byte;
        do while str1 <> 0 and str2 <> 0 and str1 = str2;
            p$str1 = p$str1 + 1;
            p$str2 = p$str2 + 1;
        end;
        return str1 = 0 and str2 = 0;
    end string$equal;


    /* READER */
    /* reader variables */
    declare reader$ptr pointer;   /* pointer to the current string */
    declare reader$chr based reader$ptr byte;
    declare reader$prev byte;
    declare reader$saved byte initial (0);

    input$end: procedure boolean;
        return reader$chr = 0;
    end input$end;

    get$char: procedure byte;
        return reader$chr;
    end get$char;

    unread$char: procedure;
        reader$saved = reader$chr;
        reader$chr = reader$prev;
    end unread$char;

    next$char: procedure;
        if reader$saved = 0 then
        do;
            reader$prev = reader$chr;
            reader$ptr = reader$ptr + 1;
        end;
        else
        do;
            reader$prev = reader$chr;
            reader$chr = reader$saved;
            reader$saved = 0;
        end;
    end next$char;


    /* TOKENIZER */
    /* tokenizer variables */
    declare token$ready boolean initial (false);
    declare token$id byte;
    declare token$value uint;
    declare token$str pointer;
    declare tbuf$len literally '80h';
    declare tok$buf (tbuf$len) byte;

    /* token ids */
    declare end$of$input literally '0';
    declare keyword literally '1';
    declare identifier literally '2';
    declare number literally '3';

    /* check whether the character is an identifier or a keyword character */
    word$char: procedure (chr) boolean;
        declare chr byte;
        if chr >= 'a' and chr <= 'z' then
            return true;
        if chr >= 'A' and chr <= 'Z' then
            return true;
/*
        if chr >= '0' and chr <= '9' then
            return true;
        if chr = '$' or chr = '_' then
            return true;
*/
        return false;
    end word$char;

    /* check whether the character is a decimal digit */
    dec$char: procedure (chr) boolean;
        declare chr byte;
        return chr >= '0' and chr <= '9';
    end dec$char;

    /* checks whether chr is equivalent to a space */
    space$char: procedure (chr) boolean;
        declare chr byte;
        return chr = space or chr = tab or chr = lf or chr = cr;
    end space$char;

    /* skips characters equivalent to a space */
    /* TODO: also skip comments */
    skip$spaces: procedure pointer;
        do while space$char(get$char);
            call next$char;
        end;
    end skip$spaces;

    /* read a string that may be an identifier or a keyword */
    read$word: procedure;
        declare buffer$ptr pointer;
        declare buffer$chr based buffer$ptr byte;
        buffer$ptr = .tok$buf;
        token$id = identifier;
        token$str = buffer$ptr;
        /* TODO: check buffer length before writing! */
        /* buffer length is in constant tbuf$len */
        do while not input$end and word$char(get$char);
            buffer$chr = get$char;
            buffer$ptr = buffer$ptr + 1;
            call next$char;
        end;
        buffer$chr = 0;
        token$ready = true;
    end read$word;

    /* read a number from the input */
    read$number: procedure ;
        declare n uint;
        n = 0;
        do while dec$char(get$char);
            n = n * 10 + get$char - '0';
            call next$char;
        end;
        token$id = number;
        token$value = n;
        token$ready = true;
    end read$number;

    read$char: procedure;
        if not input$end then
            call next$char;
        token$id = 0ffh;
        /* TODO: determine token$id based on the character */
        token$ready = true;
    end read$char;

    read$token: procedure;
        call skip$spaces;    /* skip spaces before reading the next token */
        if input$end then
            token$id = end$of$input;
        else if dec$char(get$char) then
            call read$number;
        else if word$char(get$char) then
            call read$word;
        else
            call read$char;
    end read$token;

    get$token: procedure byte;
        call print$line(.('get$token', 0));
        if not token$ready then
            call read$token;
        return token$id;
    end get$token;


    /* print token without new line */
    print$token: procedure;
        call print$string(.('TOKEN: ', 0));
        if token$id = identifier then
        do;
            call print$string(.('IDENTIFIER:', 0));
            call print$string(token$str);
        end;
        else if token$id = number then
        do;
            call print$string(.('NUMBER:', 0));
            call print$number(token$value);
        end;
        else if token$id = end$of$input then
            call print$string(.('END OF INPUT', 0));
        else
            call print$string(.('UNKNOWN TOKEN', 0));
    end print$token;

    /* MAIN PROGRAM */
    declare tail$ptr pointer;
    declare tail$chr based tail$ptr byte;
    declare tail$len byte;
    declare i byte;

    tail$ptr = tail$start;
    tail$len = tail$chr;
    tail$ptr = tail$ptr + tail$len + 1;
    tail$chr = 0;
    call print$string(.('string entered: {', 0));
    call print$string(tail$start + 1);
    call print$line(.('}', 0));
    reader$ptr = tail$start + 2;
    i = 0;
    do while i < 4 and not get$token = end$of$input;
        call print$token;
        call new$line;
        call read$token;
        i = i + 1;
    end;

end;
